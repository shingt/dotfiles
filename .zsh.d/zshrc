# -*- sh -*-

bindkey -e

setopt auto_cd
setopt auto_pushd
cdpath=(~)
chpwd_functions=($chpwd_functions dirs)

HISTFILE=~/.zsh_history
HISTSIZE=10000000
SAVEHIST=$HISTSIZE

setopt extended_history
setopt hist_ignore_dups
setopt hist_ignore_space
setopt inc_append_history
setopt share_history
setopt no_flow_control
setopt prompt_subst
setopt prompt_percent
setopt transient_rprompt

### red: 0-5
### green: 0-5
### blue: 0-5
color256()
{
    local red=$1; shift
    local green=$2; shift
    local blue=$3; shift

    echo -n $[$red * 36 + $green * 6 + $blue + 16]
}

fg256()
{
    echo -n $'\e[38;5;'$(color256 "$@")"m"
}

bg256()
{
    echo -n $'\e[48;5;'$(color256 "$@")"m"
}

###   -(user@debian)-(0)-<2011/09/01 00:54>------------------------------[/home/user]-
###   -[84](0)%                                                                   [~]
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats \
    '(%{%F{076}%}%s%{%f%k%})-[%{%F{081}%}%b%{%f%k%}]'
zstyle ':vcs_info:*' actionformats \
    '(%{%F{076}%}%s%{%f%k%})-[%{%F{081}%}%b%{%f%k%}|%{%F{red}%}%a%{%f%k%}]'

prompt_bar_left_self="%{%B%}%n%{%b%}%{%F{cyan}%}@%{%f%}%{%B%}%m%{%b%}"
prompt_bar_left_status="%{%B%F{white}%}%{%k%f%b%}"
prompt_bar_left_date=""
prompt_bar_left="${prompt_bar_left_self}-${prompt_bar_left_status}"
###   %{%B%K{magenta}%F{white}%}...%{%f%k%b%}:
prompt_bar_right="[%{%B%F{magenta}%}%d%{%f%k%b%}]"

prompt_left="%(1j,(%j),)%{%B%}%#%{%b%} "

count_prompt_characters()
{
    print -n -P -- "$1" | sed -e $'s/\e\[[0-9;]*m//g' | wc -m | sed -e 's/ //g'
}

update_prompt()
{
    local bar_left_length=$(count_prompt_characters "$prompt_bar_left")
    local bar_rest_length=$[COLUMNS - bar_left_length]
    local bar_left="$prompt_bar_left"
    local bar_right_without_path="${prompt_bar_right:s/%d//}"
    local bar_right_without_path_length=$(count_prompt_characters "$bar_right_without_path")
    local max_path_length=$[bar_rest_length - bar_right_without_path_length]
    bar_right=${prompt_bar_right:s/%d/%(C,%${max_path_length}<...<%d%<<,)/}
    local separator="${(l:${bar_rest_length}::-:)}"
    bar_right="%${bar_rest_length}<<${separator}${bar_right}%<<"
    PROMPT="${bar_left}${bar_right}"$'\n'"${prompt_left}"
    RPROMPT="[%{%B%F{magenta}%}%~%{%k%f%b%}]"
    case "$TERM_PROGRAM" in
	Apple_Terminal)
	    RPROMPT="${RPROMPT}-"
	    ;;
    esac

    LANG=C vcs_info >&/dev/null
    if [ -n "$vcs_info_msg_0_" ]; then
#	RPROMPT="${vcs_info_msg_0_}-${RPROMPT}"
#	Temprarily disable current dir display.
	RPROMPT="${vcs_info_msg_0_}"
    fi
}

precmd_functions=($precmd_functions update_prompt)

fpath=(~/.zsh.d/completion $fpath)

autoload -U compinit
compinit -u

zstyle ':completion:*' format '%B%d%b'
zstyle ':completion:*' group-name ''
zstyle ':completion:*:default' menu select=2
zstyle ':completion:*:default' list-colors ""
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' completer \
    _oldlist _complete _match _ignored _approximate _prefix
zstyle ':completion:*' use-cache yes
zstyle ':completion:*' verbose yes
zstyle ':completion:sudo:*' environ PATH="$SUDO_PATH:$PATH"

setopt complete_in_word
setopt glob_complete
setopt hist_expand
setopt no_beep
setopt numeric_glob_sort
setopt magic_equal_subst
setopt extended_glob
setopt mark_dirs
setopt long_list_jobs

REPORTTIME=3

watch="all"

setopt ignore_eof

WORDCHARS=${WORDCHARS:s,/,,}
WORDCHARS="${WORDCHARS}|"

case $(uname) in
    *BSD|Darwin)
	if [ -x "$(which gnuls)" ]; then
	    alias ls="gnuls"
	    alias la="ls -lhAF --color=auto"
	else
	    alias la="ls -lhAFG"
	fi
	alias ps="ps -fU$(whoami)"
	;;
    SunOS)
	if [ -x "`which gls`" ]; then
	    alias ls="gls"
	    alias la="ls -lhAF --color=auto"
	else
	    alias la="ls -lhAF"
	fi
	alias ps="ps -fl -u$(/usr/xpg4/bin/id -un)"
	;;
    *)
	alias la="ls -lhAF --color=auto"
	alias ps="ps -fU$(whoami) --forest"
	;;
esac

update_title() {
    local command_line=
    typeset -a command_line
    command_line=${(z)2}
    local command=
    if [ ${(t)command_line} = "array-local" ]; then
	command="$command_line[1]"
    else
	command="$2"
    fi
    print -n -P "\e]2;"
    echo -n "(${command})"
    print -n -P " %n@%m:%~\a"
}

